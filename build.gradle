/*
 * Copyright 2018 Rundeck, Inc. (http://rundeck.com)
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'groovy'
    id 'java'
}

ext.rundeckVersion='3.0.6-20180917'
defaultTasks 'clean','build'
ext.rundeckPluginVersion= '1.2'
group 'com.rundeck.plugin'

project.version= "1.0.0"

ext.pluginClassNames=
        'com.rundeck.plugins.LoadKeyStorageWorkflowStep'
ext.pluginName = 'Load key storage value'
ext.pluginDescription = 'Load the key storage value into a new variable'


configurations{
    pluginLibs

    //declare compile to extend from pluginLibs so it inherits the dependencies
    compile {
        extendsFrom pluginLibs
    }
}
repositories {
    mavenLocal()
    maven{ //snapshot dependency
        url='https://oss.sonatype.org/content/repositories/snapshots/'
    }
    mavenCentral()
}

dependencies {
    compile group: 'org.rundeck', name: 'rundeck-core', version: rundeckVersion
    compile "org.codehaus.groovy:groovy-all:2.4.15"
    compile 'log4j:log4j:1.2.17'

    testCompile 'junit:junit:4.8.1',
            'org.mockito:mockito-all:1.8.5'

    testCompile "org.codehaus.groovy:groovy-all:2.4.15"
    testCompile "cglib:cglib-nodep:2.2.2"
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
}


// task to copy plugin libs to output/lib dir
task copyToLib(type: Copy) {
    into "$buildDir/output/lib"
    from configurations.pluginLibs
}

jar {
    from "$buildDir/output"
    manifest {
        attributes 'Rundeck-Plugin-Classnames': pluginClassNames
        attributes 'Rundeck-Plugin-Name': pluginName
        attributes 'Rundeck-Plugin-Description': pluginDescription
        def libList = configurations.pluginLibs.collect { 'lib/' + it.name }.join(' ')
        attributes 'Rundeck-Plugin-Libs': "${libList}"
    }
}

//set jar task to depend on copyToLib
jar.dependsOn(copyToLib)

